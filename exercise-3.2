Theorem (15.9) “Zero of ·”: a · 0 = 0
Proof:
    a · 0 = 0
  ≡⟨ “Cancellation of +” ⟩
    a · a + a · 0 = a · a + 0
  ≡⟨ “Distributivity of · over +” ⟩
    a · (a + 0) = a · a + 0
  ≡⟨ “Identity of +” ⟩
    a · a = a · a     — This is “Reflexivity of =”
    
-------------------------

Theorem (15.10a) “Unique identity of +”: a + z = a  ≡  z = 0
Proof:
    a + z = a ≡ z = 0
  ≡⟨ “Identity of +” ⟩
    a + z = a + 0 ≡ z = 0
  ≡⟨ “Cancellation of +” ⟩
    z = 0 ≡ z = 0
  ≡⟨ “Reflexivity of ≡” ⟩
    true
-------------------------
Theorem (15.15) “Unique additive inverse”: x + a = 0  ≡  x = - a
Proof:
    x + a = 0 ≡ x = - a
  ≡⟨ “Unary minus” ⟩
    x + a = a + - a ≡ x = - a
  ≡⟨ “Cancellation of +” ⟩
    x = - a ≡ x = - a
  ≡⟨ “Reflexivity of ≡” ⟩
    true
-------------------------
Theorem (15.16) “Injectivity of unary minus”: - a = - b  ≡  a = b
Proof:
    - a = - b ≡ a = b
  ≡⟨ “Cancellation of +” ⟩
    - a + a + b = - b + a + b ≡ a = b
  ≡⟨ “Symmetry of +” ⟩
    b + a + - a = a + b + - b ≡ a = b
  ≡⟨ “Unary minus” ⟩
    b + 0 = a + 0 ≡ a = b
  ≡⟨ “Additive identity” ⟩
    b = a ≡ a = b
  ≡⟨ “Symmetry of =” ⟩
    a = b ≡ a = b
  ≡⟨ “Reflexivity of ≡” ⟩
    true
------------------------
Theorem (15.18): - 0 = 0
Proof:
    - 0 = 0
  =⟨ “Additive identity” ⟩
    0 + - 0 = 0
  =⟨ “Unary minus” ⟩
    0 = 0 — This is “Reflexivity of =”
------------------------
Theorem (15.20): - a = (- 1) · a
Proof:
    - a = (- 1) · a
  =⟨ “Additive identity” ⟩
    - a + 0 = (- 1) · a
  =⟨ “Zero of ·” ⟩
    - a + 0 · a = (- 1) · a
  =⟨ “Unary minus” ⟩
    - a + (1 + - 1) · a = (- 1) · a
  =⟨ “Distributivity of · over +” ⟩
    - a + 1 · a + (- 1) · a = (- 1) · a
  =⟨ “Multiplicative identity” ⟩
    - a + a + (- 1) · a = (- 1) · a
  =⟨ “Unary minus” ⟩
    0 + (- 1) · a = (- 1) · a
  =⟨ “Additive identity” ⟩
    (- 1) · a = (- 1) · a — This is “Reflexivity of =”
